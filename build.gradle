ext {
	cucumberVersion = "2.3.1"
	junitVersion = "4.12"
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

jar {
    baseName = 'person-service'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file("src/integration-test/java")
		}
		resources.srcDir file("src/integration-test/resources")
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

test {
	//we want display the following test events
    testLogging {
    	showStandardStreams = true
        events "failed", "standardOut", "standardError"
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    
    options {
        systemProperties(System.getProperties())
    }
    
    testLogging {
    	showStandardStreams = true
        events "failed", "standardOut", "standardError"
    }
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    
    testCompile("junit:junit:$junitVersion")
    
    // test dependencies for cucumber and cucumber junit runner
    testCompile("io.cucumber:cucumber-java8:$cucumberVersion")
    testCompile("io.cucumber:cucumber-junit:$cucumberVersion")
}